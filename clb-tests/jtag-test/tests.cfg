source ./setup.cfg
source ./init.cfg

# LUTRAM Tests

# Extract n-th bit from num
proc bit_extract {num n} {
    return [expr {($num >> $n) & 0x1}]
}

# Interleaves two n-bit integer values a and b, starting with a
# Args:
#  - n: bit length of a and b
#  - a: first integer component
#  - b: second integer component
# Returns (n*2)-bit interleaved number
proc interleave_bits {n a b} {
    set ret 0
    for {set i 0} {$i < $n} {set i [expr {$i + 1}]} {
        set ret [expr {([bit_extract $b $i] << ($i*2 + 1)) | ([bit_extract $a $i] << ($i*2)) | $ret}]
    }
    return $ret
}

# Extracts n-th bit of each sample in the samples list
# Args:
#  - samples: list of samples
#  - n: n-th bit to extract from a sample
# Returns a list of extracted 1-bit values
proc bits_mux {samples n} {
    set ret {}
    foreach sample $samples {
        lappend ret [bit_extract $sample $n]
    }
    return $ret
}

# Assemble a number from bit_list such that n-th bit of the number maps to
# n-th entry in bit_list
# Args:
#  - bit_list: list of 1-bit samples; length of list cannot exceed 64-bits.
# Returns a number created from bit_list
proc assemble_from_bit_list {bit_list} {
    set ret 0
    for {set i 0} {$i < [llength $bit_list]} {set i [expr {$i + 1}]} {
        set ret [expr {([lindex $bit_list $i] << $i) | $ret}]
    }
    return $ret
}

# Prints test results of each output data port
proc print_test_result {result_list} {
    for {set i 0} {$i < [llength $result_list]} {set i [expr {$i + 1}]} {
        echo "lutram_do\[$i\]: [lindex $result_list $i]"
    }
}

# LUTRAM INIT Tests

# Checks INIT pattern of single- or dual-read-port LUTRAM
# Args:
#  - n: bit length of LUTRAM's INIT pattern (as multiple of 32)
#  - num_outputs: 1 or 2
proc test_lutram_init {n num_outputs} {
    global lutram_init_list
    set result {}
    for {set i 0} {$i < $num_outputs} {set i [expr {$i + 1}]} {
        lappend result PASS
    }
    # compare 32-bits at a time
    set end [expr {$n / 32}]
    for {set i 0} {$i < $end} {set i [expr {$i + 1}]} {
        # INIT[(i+1)*32-1:i*32]
        set init32_pattern [lindex $lutram_init_list $i]
        set addr [expr {$i * 32}]
        set samples [read_lutram_range $addr 32]
        for {set port 0} {$port < $num_outputs} {set port [expr {$port + 1}]} {
            set lutram_pattern [assemble_from_bit_list [bits_mux $samples $port]]
            if {$init32_pattern != $lutram_pattern} {
                echo "port $port: got [format 0x%x $lutram_pattern] but expected [format 0x%x $init32_pattern]"
                lset result $port FAIL
            }
        }
    }
    return $result
}

# lutram_do[0] = O = mem[lutram_addr[4:0]] (initialized with INIT[31:0])
proc test_LUT5_init {} {
    return [test_lutram_init 32 1]
}

# lutram_do[0] = O = mem[lutram_addr[5:0]] (initialized with INIT[63:0])
proc test_LUT6_init {} {
    return [test_lutram_init 64 1]
}

# lutram_do[0] = O5 = mem[lutram_addr[4:0]] (initialized with INIT[31:0])
# lutram_do[1] = O6 = mem[lutram_addr[5:0]] (initialized with INIT[63:0])
proc test_LUT6_2_init {} {
    global lutram_init_list
    set result {PASS PASS}
    # INIT[31:0]
    set lower32_pattern [lindex $lutram_init_list 0]
    # INIT[63:32]
    set upper32_pattern [lindex $lutram_init_list 1]
    # Expect O5 port pattern to match LUT5 function
    set lut5_pattern [expr {($lower32_pattern << 32) | $lower32_pattern}]
    # Expect O6 port pattern to match LUT6 function
    set lut6_pattern [expr {($upper32_pattern << 32) | $lower32_pattern}]
    set samples [read_lutram_range 0x0 64]
    set o5_pattern [assemble_from_bit_list [bits_mux $samples 0]]
    set o6_pattern [assemble_from_bit_list [bits_mux $samples 1]]
    if {$o5_pattern != $lut5_pattern} {
        echo "port 0 (O5): got [format 0x%x $o5_pattern] but expected [format 0x%x $lut5_pattern]"
        lset result 0 FAIL
    }
    if {$o6_pattern != $lut6_pattern} {
        echo "port 1 (O6): got [format 0x%x $o6_pattern] but expected [format 0x%x $lut6_pattern]"
        lset result 1 FAIL
    }
    return $result
}

# lutram_do[0] = O5 = mem[lutram_addr[3:0]] (initialized with INIT[15:0])
# lutram_do[1] = O6 = mem[lutram_addr[4:0]] (initialized with INIT[31:0])
# test ignores lutram_do[2] = CDO = mem[31]
proc test_CFGLUT5_init {} {
    global lutram_init_list
    set result {PASS PASS}
    # INIT[31:0]
    set init32_pattern [lindex $lutram_init_list 0]
    # INIT[15:0]
    set init16_pattern [expr {$init32_pattern & 0xFFFF}]
    # Expect O5 port pattern to match LUT4 function (O5 = mem[{I3,I2,I1,I0}])
    set lut4_pattern [expr {($init16_pattern << 16) | $init16_pattern}]
    # Expect O6 port pattern to match LUT5 function (O6 = mem[{I4,I3,I2,I1,I0}])
    set lut5_pattern $init32_pattern
    set samples [read_lutram_range 0x0 32]
    set o5_pattern [assemble_from_bit_list [bits_mux $samples 0]]
    set o6_pattern [assemble_from_bit_list [bits_mux $samples 1]]
    if {$o5_pattern != $lut4_pattern} {
        echo "port 0 (O5): got [format 0x%x $o5_pattern] but expected [format 0x%x $lut4_pattern]"
        lset result 0 FAIL
    }
    if {$o6_pattern != $lut5_pattern} {
        echo "port 1 (O6): got [format 0x%x $o6_pattern] but expected [format 0x%x $lut5_pattern]"
        lset result 1 FAIL
    }
    return $result
}

# lutram_do[0] = Q = mem[lutram_addr[3:0]] (initialized with INIT[15:0])
proc test_SRL16E_init {} {
    return [test_lutram_init 16 1]
}

# lutram_do[0] = Q = mem[lutram_addr[3:0]] (initialized with INIT[15:0])
# test ignores lutram_do[1] = Q15 = mem[15]
proc test_SRLC16E_init {} {
    return [test_lutram_init 16 1]
}

# lutram_do[0] = Q = mem[lutram_addr[4:0]] (initialized with INIT[31:0])
# test ignores lutram_do[1] = Q31 = mem[31]
proc test_SRLC32E_init {} {
    return [test_lutram_init 32 1]
}

# lutram_do[0] = O = mem[lutram_addr[4:0]] (initialized with INIT[31:0])
proc test_RAMS32_init {} {
    return [test_lutram_init 32 1]
}

# lutram_do[0] = O = mem[lutram_addr[4:0]] (initialized with INIT[31:0])
proc test_RAMD32_init {} {
    return [test_RAMS32_init]
}

# lutram_do[0] = O = mem[lutram_addr[4:0]] (initialized with INIT[31:0])
proc test_RAM32X1S_init {} {
    return [test_RAMS32_init]
}

# {A4,A3,A2,A1,A0} = {DRPA4,DPRA3,DPRA2,DPRA1,DPRA0} = lutram_addr[4:0]
# lutram_do[0] = SPO = mem[lutram_addr[4:0]] (initialized with INIT[31:0])
# lutram_do[1] = DPO = mem[lutram_addr[4:0]] (initialized with INIT[31:0])
proc test_RAM32X1D_init {} {
    return [test_lutram_init 32 2]
}

# lutram_do[0] = O = mem[lutram_addr[5:0]] (initialized with INIT[63:0])
proc test_RAMS64E_init {} {
    return [test_lutram_init 64 1]
}

# lutram_do[0] = O = mem[lutram_addr[5:0]] (initialized with INIT[63:0])
proc test_RAM64X1S_init {} {
    return [test_RAMS64E_init]
}

# {A5,A4,A3,A2,A1,A0} = {DPRA5,DRPA4,DPRA3,DPRA2,DPRA1,DPRA0} = lutram_addr[5:0]
# lutram_do[0] = SPO = mem[lutram_addr[5:0]] (initialized with INIT[63:0])
# lutram_do[1] = DPO = mem[lutram_addr[5:0]] (initialized with INIT[63:0])
proc test_RAM64X1D_init {} {
    return [test_lutram_init 64 2]
}

# lutram_do[0] = O = mem[lutram_addr[6:0]] (initialized with INIT[127:0])
proc test_RAM128X1S_init {} {
    return [test_lutram_init 128 1]
}

# A[6:0] = DPRA[6:0] = lutram_addr[6:0]
# lutram_do[0] = SPO = mem[lutram_addr[6:0]] (initialized with INIT[127:0])
# lutram_do[1] = DPO = mem[lutram_addr[6:0]] (initialized with INIT[127:0])
proc test_RAM128X1D_init {} {
    # RADR=WADR
    return [test_lutram_init 128 2]
}

# lutram_do[0] = O = mem[lutram_addr[7:0]] (initialized with INIT[255:0])
proc test_RAM256X1S_init {} {
    return [test_lutram_init 256 1]
}

# ADDRA[4:0] = ADDRB[4:0] = ADDRC[4:0] = ADDRD[4:0] = lutram_addr[4:0]
# lutram_do[1:0] = DOD[1:0] = mem_d[2*lutram_addr[4:0]+:2] (initialized with INIT[63:0]
# lutram_do[3:2] = DOC[1:0] = mem_c[2*lutram_addr[4:0]+:2] (initialized with INIT[127:64]
# lutram_do[5:4] = DOB[1:0] = mem_b[2*lutram_addr[4:0]+:2] (initialized with INIT[191:128]
# lutram_do[7:6] = DOA[1:0] = mem_a[2*lutram_addr[4:0]+:2] (initialized with INIT[255:192]
proc test_RAM32M_init {} {
    global lutram_init_list
    set result {PASS PASS PASS PASS PASS PASS PASS PASS}
    # compare 32-bits at a time
    for {set i 0} {$i < 2} {set i [expr {$i + 1}]} {
        set addr [expr {$i * 16}]
        set samples [read_lutram_range $addr 16]
        # For each port group
        for {set port_group 0} {$port_group < 4} {set port_group [expr {$port_group + 1}]} {
            set offset [expr {$port_group * 64}]
            # INIT[(i+1)*32 + offset - 1:i*32 + offset]
            set init_index [expr {($offset / 32) + $i}]
            set init32_pattern [lindex $lutram_init_list $init_index]

            set lower16_pattern [assemble_from_bit_list [bits_mux $samples [expr {$port_group*2}]]]
            set upper16_pattern [assemble_from_bit_list [bits_mux $samples [expr {$port_group*2+1}]]]
            set lutram32_pattern [interleave_bits 16 $lower16_pattern $upper16_pattern]
            if {$init32_pattern != $lutram32_pattern} {
                echo "port group $port_group: got [format 0x%x $lutram32_pattern] but expected [format 0x%x $init32_pattern]"
                lset result [expr {$port_group*2}] FAIL
                lset result [expr {$port_group*2+1}] FAIL
            }
        }
    }
    return $result
}

# ADDRA[5:0] = ADDRB[5:0] = ADDRC[5:0] = ADDRD[5:0] = lutram_addr[5:0]
# lutram_do[0] = DOD = mem_d[lutram_addr[5:0]] (initialized with INIT[63:0])
# lutram_do[1] = DOC = mem_c[lutram_addr[5:0]] (initialized with INIT[127:64])
# lutram_do[2] = DOB = mem_b[lutram_addr[5:0]] (initialized with INIT[191:128])
# lutram_do[3] = DOA = mem_a[lutram_addr[5:0]] (initialized with INIT[255:192])
proc test_RAM64M_init {} {
    global lutram_init_list
    set result {PASS PASS PASS PASS}
    # compare 32-bits at a time
    for {set i 0} {$i < 2} {set i [expr {$i + 1}]} {
        set addr [expr {$i * 32}]
        set samples [read_lutram_range $addr 32]
        # For each port group
        for {set port_group 0} {$port_group < 4} {set port_group [expr {$port_group + 1}]} {
            set offset [expr {$port_group * 64}]
            # INIT[(i+1)*32 + offset - 1:i*32 + offset]
            set init_index [expr {($offset / 32) + $i}]
            set init32_pattern [lindex $lutram_init_list $init_index]

            set lutram_pattern [assemble_from_bit_list [bits_mux $samples $port_group]]
            if {$init32_pattern != $lutram_pattern} {
                echo "port group $port_group: got [format 0x%x $lutram_pattern] but expected [format 0x%x $init32_pattern]"
                lset result $port_group FAIL
            }
        }
    }
    return $result
}

echo "tests.cfg loaded"
